# SPDX-License-Identifier: LGPL-3.0-or-later
#
#  Copyright (C) 2021 Perry Werneck <perry.werneck@gmail.com>
#
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU Lesser General Public License as published
#  by the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU Lesser General Public License
#  along with this program.  If not, see <https://www.gnu.org/licenses/>.
#

!include "MUI2.nsh"
!include "x64.nsh"
!include "FileFunc.nsh"
!include "LogicLib.nsh"
!include "winmessages.nsh"

Name "pw3270"
Caption "pw3270 - IBM 3270 Terminal emulator for GTK"
outfile "pw3270-@PACKAGE_VERSION_MAJOR@.@PACKAGE_VERSION_MINOR@.@PACKAGE_VERSION_MICRO@-@host_cpu@.exe"

XPStyle on

installDir "$PROGRAMFILES64\pw3270"

#define the installer icon
!define MUI_ICON "bin\pw3270.ico"
!define MUI_UNICON "bin\pw3270.ico"
icon "bin\pw3270.ico"

# Get installation folder from registry if available
InstallDirRegKey HKLM "Software\pw3270" "InstallLocation"

RequestExecutionLevel admin

# Properties
VIProductVersion "@PACKAGE_VERSION_MAJOR@.@PACKAGE_VERSION_MINOR@.@PACKAGE_VERSION_MICRO@"
VIFileVersion "@PACKAGE_VERSION_MAJOR@.@PACKAGE_VERSION_MINOR@.@PACKAGE_VERSION_MICRO@"

# Reference: https://nsis.sourceforge.io/Reference/VIAddVersionKey
VIAddVersionKey "ProductVersion" "@PACKAGE_VERSION_MAJOR@.@PACKAGE_VERSION_MINOR@.@PACKAGE_VERSION_MICRO@"
VIAddVersionKey "FileVersion" "@PACKAGE_VERSION_MAJOR@.@PACKAGE_VERSION_MINOR@.@PACKAGE_VERSION_MICRO@"

VIAddVersionKey "ProductName" "pw3270"
VIAddVersionKey "FileDescription" IBM 3270 Terminal emulator for GTK
VIAddVersionKey "LegalCopyright" "(C) 2017 Banco do Brasil S/A. All Rights Reserved"
# VIAddVersionKey "PrivateBuild" ""

# Interface

!define MUI_ABORTWARNING
!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "share\pw3270\LICENSE"
!insertmacro MUI_PAGE_COMPONENTS
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES

!insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_UNPAGE_FINISH

# Languages
!insertmacro MUI_LANGUAGE "English"

# Section scripts
!include Sections.nsh

# default section
SubSection "pw3270" SecMain

	Section "pw3270" SecCore

		SetRegView 64
		${DisableX64FSRedirection}

		# Set SMPROGRAMS and DESKTOP path
		SetShellVarContext all

		# Binary files
		setOutPath "$INSTDIR"
		file /r "bin"
		
		setOutPath "$INSTDIR\share\pw3270"
		file /r "share\pw3270\*.png"
		file /r "share\pw3270\*.svg"
		file /r "share\pw3270\*.ui.xml"
		file /r "share\pw3270\*.conf"

		setOutPath "$INSTDIR\share\pw3270\remap"
		file /r "share\pw3270\remap\*"

		setOutPath "$INSTDIR\lib\gdk-pixbuf-2.0"
		file /r "lib\gdk-pixbuf-2.0\*"

		setOutPath "$INSTDIR\etc"
		file /r "etc\*"

		setOutPath "$INSTDIR\share\locale"
		file /r "share\locale\*"

		setOutPath "$INSTDIR\share\themes"
		file /r "share\themes\*"

		setOutPath "$INSTDIR\share\icons"
		file /r "share\icons\*"

		setOutPath "$INSTDIR\share\glib-2.0"
		file /r "share\glib-2.0\*"

		setOutPath "$INSTDIR\share\pw3270\icons"
		file /r "share\pw3270\icons\*"
		
		# define uninstaller name
		SetRegView 32

		writeUninstaller $INSTDIR\uninstall.exe

		WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\pw3270" \
			         "DisplayName" "pw3270 - IBM 3270 Terminal emulator for GTK"
		WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\pw3270" \
			         "DisplayIcon" "$INSTDIR\pw3270.ico"
		WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\pw3270" \
			         "DisplayVersion" "@PACKAGE_VERSION_MAJOR@.@PACKAGE_VERSION_MINOR@.@PACKAGE_VERSION_MICRO@"

		WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\pw3270" \
			         "UninstallString" "$INSTDIR\uninstall.exe"
		WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\pw3270" \
			         "InstallLocation" "$INSTDIR"
		WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\pw3270" \
			         "NoModify" "1"
		WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\pw3270" \
			         "NoRepair" "1"

		# Default settings
		SetRegView 64

		# Setup log file
		# https://docs.microsoft.com/en-us/windows/win32/eventlog/event-sources
		WriteRegStr HKLM "SYSTEM\CurrentControlSet\Services\EventLog\pw3270" \
				 "PackageVersion"	"@PACKAGE_VERSION@"

		# Required for IPC Library.
		WriteRegStr 	HKLM	"Software\pw3270"				"InstallLocation"	"$INSTDIR"

		# Customized options
		WriteRegDWORD	HKLM	"Software\pw3270\toggles"		"autoconnect"			0x00000001
		WriteRegDWORD	HKLM	"Software\pw3270\toggles"		"blankfill"             0x00000000
		WriteRegDWORD	HKLM	"Software\pw3270\toggles"		"bold"                  0x00000000
		WriteRegDWORD	HKLM	"Software\pw3270\toggles"		"keepselected"          0x00000001
		WriteRegDWORD	HKLM	"Software\pw3270\toggles"		"marginedpaste"         0x00000001
		WriteRegDWORD	HKLM	"Software\pw3270\toggles"		"rectselect"            0x00000000

		WriteRegDWORD	HKLM	"Software\pw3270\toggles"		"monocase"              0x00000000
		WriteRegDWORD	HKLM	"Software\pw3270\toggles"		"cursorblink"           0x00000001
		WriteRegDWORD	HKLM	"Software\pw3270\toggles"		"showtiming"            0x00000001
		WriteRegDWORD	HKLM	"Software\pw3270\toggles"		"cursorpos"             0x00000001
		WriteRegDWORD	HKLM	"Software\pw3270\toggles"		"linewrap"              0x00000000
		WriteRegDWORD	HKLM	"Software\pw3270\toggles"		"crosshair"             0x00000000
		WriteRegDWORD	HKLM	"Software\pw3270\toggles"		"fullscreen"            0x00000000
		WriteRegDWORD	HKLM	"Software\pw3270\toggles"		"autoreconnect"         0x00000001
		WriteRegDWORD	HKLM	"Software\pw3270\toggles"		"insert"                0x00000000
		WriteRegDWORD	HKLM	"Software\pw3270\toggles"		"smartpaste"            0x00000001
		WriteRegDWORD	HKLM	"Software\pw3270\toggles"		"beep"                  0x00000001
		WriteRegDWORD	HKLM	"Software\pw3270\toggles"		"fieldattr"             0x00000000
		WriteRegDWORD	HKLM	"Software\pw3270\toggles"		"altscreen"             0x00000001
		WriteRegDWORD	HKLM	"Software\pw3270\toggles"		"keepalive"             0x00000000

		WriteRegDWORD	HKLM	"Software\pw3270\toggles"		"dstrace"               0x00000000
		WriteRegDWORD	HKLM	"Software\pw3270\toggles"		"screentrace"           0x00000000
		WriteRegDWORD	HKLM	"Software\pw3270\toggles"		"eventtrace"            0x00000000
		WriteRegDWORD	HKLM	"Software\pw3270\toggles"		"nettrace"              0x00000000
		WriteRegDWORD	HKLM	"Software\pw3270\toggles"		"ssltrace"              0x00000000

		WriteRegStr 	HKLM	"Software\pw3270"				"font-family"			"Lucida Console"
		WriteRegStr		HKLM	"Software\pw3270"				"colors"				"rgb(24,24,24);rgb(79,156,254);rgb(237,74,70);rgb(235,110,183);rgb(131,199,70);rgb(86,216,201);rgb(239,197,65);rgb(222,222,222);rgb(59,59,59);rgb(54,142,171);rgb(250,145,83);rgb(165,128,226);rgb(112,180,51);rgb(65,199,185);rgb(219,179,45);rgb(119,119,119);rgb(131,199,70);rgb(237,74,70);rgb(65,199,185);rgb(250,145,83);rgb(37,37,37);rgb(222,222,222);rgb(222,222,222);rgb(24,24,24);rgb(222,222,222);rgb(79,156,254);rgb(131,199,70);rgb(239,197,65);rgb(239,197,65)"

		createShortCut "$SMPROGRAMS\pw3270.lnk" "$INSTDIR\bin\pw3270.exe" "" "$INSTDIR\bin\pw3270.ico"
		createShortCut "$DESKTOP\pw3270.lnk" "$INSTDIR\bin\pw3270.exe" "" "$INSTDIR\bin\pw3270.ico"

	sectionEnd

!ifdef WITHSDK
    Section /o "Software Development Kit" SDK

		setOutPath $INSTDIR\sdk\include
		file /r "include\*.*"

		setOutPath $INSTDIR\sdk\lib
		file /r "lib\*.a"

		setOutPath $INSTDIR\sdk\lib\pkgconfig
		file /r "lib\pkgconfig\*.pc"

		setOutPath $INSTDIR\sdk\msvc
		file /r "share\pw3270\def\*.def"
		file /r "share\pw3270\def\*.mak"

		SetRegView 64
		WriteRegExpandStr HKLM "SYSTEM\CurrentControlSet\Control\Session Manager\Environment" "PW3270_SDK_PATH" "$INSTDIR\sdk"
		SendMessage ${HWND_BROADCAST} ${WM_WININICHANGE} 0 "STR:Environment" /TIMEOUT=5000

    SectionEnd
!endif

!ifdef WITHCERTS
	Section "SSL Certificates" SSLCerts
		setOutPath $INSTDIR\certs
		file /r "sslcerts\*.*"
	sectionEnd
!endif

	SubSection "Plugins" SecPLugin

		Section "Remote control" IPCPlugin

			${DisableX64FSRedirection}

			CreateDirectory "$INSTDIR\lib\pw3270-plugins"
            file "/oname=$INSTDIR\lib\pw3270-plugins\ipcserver.dll" "lib\pw3270-plugins\ipcserver.dll"
	
		sectionEnd

	SubSectionEnd

SubSectionEnd

Section "Uninstall"

	# Always delete uninstaller first
	delete $INSTDIR\uninstaller.exe

	# Remove association

	# Set SMPROGRAMS and DESKTOP path
	SetShellVarContext all

	# now delete installed files
	delete $INSTDIR\pw3270.exe
	delete $SMPROGRAMS\pw3270.lnk
	delete $DESKTOP\pw3270.lnk

	RMDir /r "$INSTDIR"

	# Remove registry
	SetRegView 32
	DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\pw3270"
	DeleteRegKey HKLM "Software\pw3270"
	
	SetRegView 64
	DeleteRegKey HKLM "Software\pw3270"
	DeleteRegValue HKLM "SYSTEM\CurrentControlSet\Control\Session Manager\Environment" "PW3270_SDK_PATH"
	DeleteRegKey HKLM "SYSTEM\CurrentControlSet\Services\EventLog\pw3270"

	SendMessage ${HWND_BROADCAST} ${WM_WININICHANGE} 0 "STR:Environment" /TIMEOUT=5000

	DeleteRegKey HKLM "Software\GSettings\apps\pw3270\pw3270"

	# Delete System libraries
	${DisableX64FSRedirection}

	delete $SYSDIR\libipc3270.dll

!ifdef WITHLIBHLLAPI
	delete $SYSDIR\libhllapi.dll
	delete $SYSDIR\hllapi.dll
!endif

!ifdef WITHMONO-TN3270
	delete $SYSDIR\mono-tn3270.dll
!endif

	RMDir /r "$INSTDIR"

SectionEnd

Function .onInit

	#---[ Check if already installed ]--------------------------------------------------------------------

	${If} ${FileExists} "$INSTDIR\pw3270.exe"
		MessageBox MB_OK|MB_ICONEXCLAMATION "pw3270 is already installed. Please uninstall it first" /SD IDOK
		Abort
	${EndIf}

	#---[ Check PLUGINS Command line option ]-------------------------------------------------------------

	Push $0

	${GetParameters} $R0
	ClearErrors
	${GetOptions} $R0 /PLUGINS= $0

	${if} $0 == "NO"

!ifdef WITHMONO-TN3270
		SectionGetFlags ${DOTNET} $0
		IntOp $0 $0 & ${SECTION_OFF}
		SectionSetFlags ${DOTNET} $0
!endif

!ifdef WITHLIBHLLAPI
		SectionGetFlags ${HLLAPIBinding} $0
		IntOp $0 $0 | ${SF_SELECTED}
		SectionSetFlags ${HLLAPIBinding} $0
!endif

		SectionGetFlags ${IPCPlugin} $0
		IntOp $0 $0 | ${SF_SELECTED}
		SectionSetFlags ${IPCPlugin} $0

	${EndIf}

	Pop $0

	#---[ Check SDK Command line option ]-----------------------------------------------------------------

!ifdef WITHSDK

	Push $0

	${GetParameters} $R0
	ClearErrors
	${GetOptions} $R0 /SDK= $0

	${if} $0 == "YES"

		SectionGetFlags ${SDK} $0
		IntOp $0 $0 | ${SF_SELECTED}
		SectionSetFlags ${SDK} $0

	${else}

		SectionGetFlags ${SDK} $0
		IntOp $0 $0 & ${SECTION_OFF}
		SectionSetFlags ${SDK} $0

	${EndIf}

	Pop $0
!endif

	#---[ Check DOTNET Command line option ]--------------------------------------------------------------

!ifdef WITHMONO-TN3270
	Push $0

	${GetParameters} $R0
	ClearErrors
	${GetOptions} $R0 /DOTNET= $0

	# Default = NO
	${if} $0 == "YES"

		SectionGetFlags ${DOTNET} $0
		IntOp $0 $0 | ${SF_SELECTED}
		SectionSetFlags ${DOTNET} $0

		SectionGetFlags ${IPCPlugin} $0
		IntOp $0 $0 | ${SF_SELECTED}
		SectionSetFlags ${IPCPlugin} $0

	${else}

		SectionGetFlags ${DOTNET} $0
		IntOp $0 $0 & ${SECTION_OFF}
		SectionSetFlags ${DOTNET} $0

	${EndIf}

	Pop $0
!endif

	#---[ Check HLLAPI Command line option ]-------------------------------------------------------------

!ifdef WITHLIBHLLAPI

	Push $0

	${GetParameters} $R0
	ClearErrors
	${GetOptions} $R0 /HLLAPI= $0

	# Default = YES
	${if} $0 == "NO"

		SectionGetFlags ${HLLAPIBinding} $0
		IntOp $0 $0 & ${SECTION_OFF}
		SectionSetFlags ${HLLAPIBinding} $0

	${else}

		SectionGetFlags ${HLLAPIBinding} $0
		IntOp $0 $0 | ${SF_SELECTED}
		SectionSetFlags ${HLLAPIBinding} $0

		SectionGetFlags ${IPCPlugin} $0
		IntOp $0 $0 | ${SF_SELECTED}
		SectionSetFlags ${IPCPlugin} $0


	${EndIf}

	Pop $0

!endif

FunctionEnd

Function .onInstSuccess

	# Update GTK Image loaders
	

FunctionEnd

